; ----------------------------- INTERRUPT MANAGEMENT -----------------------------

; begin globals
_SYS_INT__DEFAULT_HANDLER_PTR: WORD $_SYS_INT__DEFAULT_HANDLER
_SYS_INT__DEFAULT_PS: WORD 0x180
; end globals


; func DEFAULT_HANDLER: empty interrupt handler
; begin func
_SYS_INT__DEFAULT_HANDLER:
  IRET
; end func


; func REGISTER_INT_HANDLER: (addr) registers interrupt vector for first free slot
; 
; params: AC: addr: handler addr 
; return: vector number if OK, 0xFFFF if no handlers left
; stack:
; &: target
; &: cur_id
; &: ret_addr
; begin globals
RIH__HANDLERS_PTR: WORD 0x0
; end globals
; begin func
REGISTER_INT_HANDLER:
; begin prologue
  APUSH
; end prologue
  ST &target

  LD #0
  ST &cur_id

  RIH__LOOP:
    LD &cur_id
    CMP #8
    BHIS RIH__NO_FREE

    ASL
    ST RIH__HANDLERS_PTR
    LD (RIH__HANDLERS_PTR)
    CMP $_SYS_INT__DEFAULT_HANDLER_PTR
    BEQ RIH__SET

    LD &cur_id
    INC
    ST &cur_id
    JUMP RIH__LOOP

  ; no free handlers
RIH__NO_FREE:
  LD #0xFF
  ST &cur_id
  JUMP RIH__EXIT

RIH__SET:  
  LD &target ; set vector
  ST (RIH__HANDLERS_PTR)+

  LD $_SYS_INT__DEFAULT_PS
  ST (RIH__HANDLERS_PTR)
  
RIH__EXIT:
; begin epilogue
  ; cur_id is last pop
  APOP
  RET
; end epilogue
; end func REGISTER_INT_HANDLER



; func UNREGISTER_INT_HANDLER: (id) unregister interrupt vector
; 
; params: AC: id: id of vector
; begin globals
UIH__HANDLERS_PTR: WORD ?
; end globals
; begin func
UNREGISTER_INT_HANDLER:
  ASL
  ST UIH__HANDLERS_PTR

  LD $_SYS_INT__DEFAULT_HANDLER_PTR
  ST (UIH__HANDLERS_PTR)+

  LD $_SYS_INT__DEFAULT_PS
  ST (UIH__HANDLERS_PTR)
  RET
; end func UNREGISTER_INT_HANDLER



; func INTERRUPTS_INIT: () initializes interrupts to default values.
; stack:
; &: cur_id
; begin func
INTERRUPTS_INIT:
; begin prologue
  APUSH
; end prologue

  LD #8
  ST &cur_id

  INTI__LOOP:
    LD &cur_id
    DEC
    CALL $UNREGISTER_INT_HANDLER
    LOOP &cur_id
    JUMP INTI__LOOP

INTI__END:
; begin epilogue
  APOP
  RET
; end epilogue
; end func INTERRUPTS_INIT
