; ----------------------------- CONTEXT SWITCH -----------------------------

; func OS__CONTEXT_SWITCH_INT: () interrupt to swtich current execution context.
; Stores current process state to TCB and restores next process state then jumps to it
; Should be called with timer
; 
; begin globals
OS__CONTEXT_SWITCH_INT_PTR: WORD $OS__CONTEXT_SWITCH_INT
; end globals
; begin func
OS__CONTEXT_SWITCH_INT:
  ; here is PS on stack top an IP the next
  PUSH ; cur accumulator value
  
  LD $_SYS_PROC__RUNNING_PROCESS_ID  
  FCALL ($PROC_SAVE_STATE_PTR)

  ; search for active process
  LD $_SYS_PROC__RUNNING_PROCESS_ID
  PUSH
  CLA ; 0=running
  FCALL ($PROC_GET_NEXT_WITH_STATUS_PTR)
  SWAP
  POP
  BPL OSCSI__CONT
  FCALL ($OS__PANIC_PTR)

OSCSI__CONT:
  ST $_SYS_PROC__RUNNING_PROCESS_ID
  FCALL ($PROC_LOAD_STATE_PTR)

  ; this instruction should not be reached in normal process loading
  IRET
; end func OS__CONTEXT_SWITCH_INT



; func OS__SETUP_SWITCH_TIMER: (period)
; 
; params: AC: period (real period = period * 100ms)
; stack:
; &: period
; &: ret_addr
; begin func
EXPORT OS__SETUP_SWITCH_TIMER:
; begin prologue
  APUSH
; end prologue

  ST &period

  LD OS__CONTEXT_SWITCH_INT_PTR
  FCALL ($INT__REGISTER_HANDLER_PTR)

  PUSH
  CLA ; timer DEV#0
  FCALL ($DEV__INT_ON_PTR)
  POP
  
  CLA ; DEV#0 DR = 0
  PUSH
  LD &period+1
  FCALL ($DEV__OUT_PTR)
  POP

  EI

; begin epilogue
  APOP
  RET
; end epilogue
; end func OS__SETUP_SWITCH_TIMER

