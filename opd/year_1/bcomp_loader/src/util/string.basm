; ----------------------------- STRING UTILITIES LIBRARY -----------------------------
; string format: (char[1] char[0]) ... (char[i+1]@1b char[i]@1b) (0x00, 0x00/char[2n-1])


; func ATOI: (src, radix) converts string number to int 
; 
; params: AC src: pointer to NUL-ended string
;         STACK radix: number radix
; return: AC: unsigned 2byte integer
; stack:  
; &: str_ptr
; &: cur_radix_pow
; &: index
; &: byte_cnt
; &: src
; &: sum
; &: ret_addr
; &: radix
; begin globals
ATOI__LOW_MASK: WORD 0xFF
; end globals
; begin func
EXPORT ATOI:
; begin prologue
  APUSH
; end prologue

  ST &src
  FCALL ($STRLEN_PTR)
  ST &byte_cnt

  LD &byte_cnt ; symbol number
  ADD #2
  ASR
  ASL 
  ST &byte_cnt ; count zero bytes (one or two NUL due char count partity)

  ; start interation from last symbol (last byte) 
  ASR ; bytes to words
  ADD &src
  ST &str_ptr

  CLA 
  ST &sum
  ST &index
  INC
  ; 1, radix, radix^2, ...
  ST &cur_radix_pow

  ; reading from back. high then low bytes
  ; begin loop
  ATOI__LOOP:
    LD &index
    CMP &byte_cnt
    BEQ ATOI__END        
    INC
    ST &index

    ; begin if index % 2
    ROR
    BCC ATOI__ODD_ITER
    ; begin branch even iter -> high byte
      LD -(&str_ptr)
      SWAB
      AND ATOI__LOW_MASK
      BZS ATOI__LOOP
      JUMP ATOI__PROCESS_CHAR
    ; begin branch odd iter -> low byte
    ATOI__ODD_ITER:
      LD (&str_ptr)
      AND ATOI__LOW_MASK
      BZS ATOI__LOOP
    ; end if

    ATOI__PROCESS_CHAR:
    FCALL ($ATOI_CHAR_PTR)

    ; sum += cur_radix_pow * ATOI(char[i])
    PUSH
    LD &cur_radix_pow+1
    FCALL ($MUL_PTR)
    ; save sum
    ADD &sum+1
    ST &sum+1
    POP

    ; cur_radix_pow *= radix
    LD &radix
    PUSH
    LD &cur_radix_pow+1
    FCALL ($MUL_PTR)
    ST &cur_radix_pow+1
    POP

    JUMP ATOI__LOOP
  ; end loop

  ATOI__END:
; begin epilogue
  LD &sum
  APOP
  RET
; end epilogue
; end func ATOI


; func ATOI_CHAR: (char) returns integer representation of character with radix up to 36
; uses sequence 0,1,...,9,A(a),B(b),...,Z(z)
; params: AC char: input character
; return: AC: int value 
; begin func
EXPORT ATOI_CHAR:
  ; begin if
  CMP #0x3A
  BLT ATOI_CHAR__ISNUM
  ; begin branch char >= 10
    SUB #0x37 ; A=0x41, A is 10
    
    ; begin if
    CMP #0x2A ; A=0x41, a = 0x61; 0x61-0x37 = 0x2A
    BLT ATOI_CHAR__END
    ; begin branch char in [a..z]
      SUB #0x20
      JUMP ATOI_CHAR__END
    ; end if
  ; begin branch char < 10
  ATOI_CHAR__ISNUM:
    SUB #0x30
  ; end if
ATOI_CHAR__END:
  RET
; end func ATOI_CHAR


; func ITOA_CHAR: (val) returns character representation of one digit in some radix up to 36
; uses sequence 0,1,...,9,A,B,...,Z
; params: AC val: integer input digit
; return: AC: char value 
; begin func
EXPORT ITOA_CHAR:
  ; begin if
  CMP #0xA
  BLT ITOA_CHAR__ISNUM
  ; begin branch char >= 10
    ADD #0x37 ; A=0x41, A is 10
    JUMP ITOA_CHAR__END
  ; begin branch char < 10
  ITOA_CHAR__ISNUM:
    ADD #0x30
  ; end if
ITOA_CHAR__END:
  RET
; end func ITOA_CHAR



; func STRLEN: (ptr) get length of nul-ended string
; 
; params: AC: ptr: ponter 
; return: AC: length in symbols
; stack:
; &: ptr
; &: cnt
; &: ret_addr
; begin globals
SLEN__HMASK: WORD 0xFF00
SLEN__LMASK: WORD 0xFF
; end globals
; begin func
EXPORT STRLEN:
; begin prologue
  APUSH
; end prologue

  ST &ptr
  CLA
  ST &cnt

  ; begin loop
  SLEN__LOOP:
    LD (&ptr)
    AND SLEN__LMASK
    BZS SLEN__END

    LD (&ptr)+
    ; begin if 
    AND SLEN__HMASK
    BZS SLEN__ADD_ONE
    ; begin branch if 0xYYZZ
      LD &cnt
      ADD #2
      ST &cnt
      JUMP SLEN__LOOP
    ; begin branch if 0x00ZZ
    SLEN__ADD_ONE:
      LD &cnt
      INC
      ST &cnt
      JUMP SLEN__END
    ; end if
  ; end loop
    
  SLEN__END:
; begin epilogue
  LD &cnt
  ; ret cnt
  APOP
  RET
; end epilogue
; end func STRLEN



; func STRCMP: (str1, str2)
; 
; params: AC: str1 pointer
;         STACK: str2 pointer
; return: 0 if equal else not equal
; stack:
; &: char2
; &: char1
; &: str1
; &: ret_addr
; &: str2
; begin globals
STRCMP__MASK: WORD 0xFF00
; end globals
; begin func
EXPORT STRCMP:
; begin prologue
  APUSH
; end prologue
  ST &str1

  STRCMP__LOOP:
    LD (&str1)+
    ST &char1
    LD (&str2)+
    ST &char2

    CMP &char1
    BNE STRCMP__NOT_EQUAL
    
    ; AC=char2
    AND STRCMP__MASK
    BZS STRCMP__S2_END
    LD &char1
    AND STRCMP__MASK
    BZS STRCMP__NOT_EQUAL ; diff length
    ; begin branch char2!=NUL, char1!=NUL
      JUMP STRCMP__LOOP
    ; begin branch char2==NUL
    STRCMP__S2_END:
      LD &char1
      AND STRCMP__MASK
      BZC STRCMP__NOT_EQUAL ; diff length
      ; begin branch char2==NUL, char1==NUL
      JUMP STRCMP__EQUAL

  STRCMP__EQUAL:
    CLA
    JUMP STRCMP__END

  STRCMP__NOT_EQUAL:
    LD #1

  STRCMP__END:
; begin epilogue
  APOP
  RET
; end epilogue
; end func STRCMP

