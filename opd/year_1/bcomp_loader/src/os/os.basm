; ----------------------------- OS -----------------------------


; func OS__INIT: OS start procedure
; 
; params:
; return: 
; stack:
; &: ret_addr
; begin globals
OSI__INIT_STR: WORD "Initializing..."
OSI__START_STR: WORD "System started"
OSI__END_STR: WORD "System halt"
; end globals
; begin func
OS__INIT:
; begin prologue
  APUSH
; end prologue

  CALL $PRINT_CLEAR
  LD OSI__INIT_STR_PTR
  CALL $PRINTLN

  ; reserve processes array
  LD $_SYS_PROC__PROCESSES_MAX
  CALL $MALLOC
  ST $_SYS_PROC__PROCESSES_LIST_PTR

  ; set userspace begin
  LD $_SYS__USERSPACE_BEGIN
  ST $_SYS_PROC__FREE_MEM_START

  ; begin block setup interrupts section
  CALL $INT__INIT

  LD #1 ; 10Hz
  CALL $OS__SETUP_SWITCH_TIMER
  ; EI
  ; end block ints

  LD OSI__START_STR_PTR
  CALL $PRINTLN
  CALL $PRINT_ENDL

  CALL $SHELL_MAIN

  LD OSI__END_STR_PTR
  CALL $PRINTLN
  
; begin epilogue
  APOP
  RET
; end epilogue
; end func OS__INIT



; func OS__CONTEXT_SWITCH_INT: () interrupt to swtich current execution context.
; Stores current process state to TCB and restores next process state then jumps to it
; Should be called with timer
; 
; begin globals
OS__CONTEXT_SWITCH_INT_PTR: WORD $OS__CONTEXT_SWITCH_INT
OS__CSI__TEXT: WORD "switch"
; end globals
; begin func
OS__CONTEXT_SWITCH_INT:
  ; here is PS on stack top an IP the next
  PUSH ; cur accumulator value

  LD OS__CSI__TEXT_PTR
  CALL $PRINTLN

  LD $_SYS_PROC__RUNNING_PROCESS_ID  
  CALL $PROC_SAVE_STATE
  
  ; round-robin
  LD $_SYS_PROC__RUNNING_PROCESS_ID
  INC
  CMP $_SYS_PROC__CUR_PROCESSES_COUNT
  BLO OSCSI__CONTINUE
  CLA
  OSCSI__CONTINUE:
  ST $_SYS_PROC__RUNNING_PROCESS_ID

  CALL $PROC_LOAD_STATE

  IRET ; this instruction should not be reached in normal situaltion

; begin epilogue
  APOP
  POP
  IRET
; end epilogue
; end func OS__CONTEXT_SWITCH_INT



; func OS__SETUP_SWITCH_TIMER: (period)
; 
; params: AC: period (real period = period * 100ms)
; stack:
; &: period
; &: ret_addr
; begin func
OS__SETUP_SWITCH_TIMER:
; begin prologue
  APUSH
; end prologue

  ST &period

  LD $OS__CONTEXT_SWITCH_INT_PTR
  CALL $INT__REGISTER_HANDLER

  PUSH
  CLA ; timer DEV#0
  CALL $DEV__INT_ON
  POP
  
  CLA ; DEV#0 DR = 0
  PUSH
  LD &period+1
  CALL $DEV__OUT
  POP

; begin epilogue
  APOP
  RET
; end epilogue
; end func OS__SETUP_SWITCH_TIMER

