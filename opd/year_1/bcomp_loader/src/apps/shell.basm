; ----------------------------- BASIC SHELL INTERPRETER -----------------------------

; begin globals
_SHELL__CMD1_NAME: WORD "exit"
_SHELL__CMD2_NAME: WORD "help"
_SHELL__CMD3_NAME: WORD "load"
_SHELL__CMD_NAME_PTR_LIST: WORD $_SHELL__CMD1_NAME, $_SHELL__CMD2_NAME, $_SHELL__CMD3_NAME
_SHELL__CMD_NAME_PTR_LIST_PTR: WORD $_SHELL__CMD_NAME_PTR_LIST
_SHELL__CMD_ADDR_LIST: WORD 0xFFFF, $SHELL_CMD_HELP, $PROC_START
_SHELL__CMD_ADDR_LIST_PTR: WORD $_SHELL__CMD_ADDR_LIST
_SHELL__CMD_LIST_CNT: WORD 0x3
; end globals




; func SHELL_PROC: ()
; begin globals
SHELL_PROC_PTR: WORD $SHELL_PROC
; end globals
; begin func
SHELL_PROC:
  CALL SHELL_MAIN
  FCALL ($PROC_EXIT_PTR)
; end func SHELL_PROC



; func SHELL_MAIN: () simple shell for execution of predefined commands.
; Uses _SHELL__CMD_NAME_PTR_LIST as list of known command name strings, 
; and _SHELL__CMD_NAME_PTR_LIST as list of CALL targets or 0xFFFF for exit
; 
; stack:
; &: ptr
; &: input_ptr
; &: ret_addr
; begin globals
SHELL__START_STR: WORD "Hello. You are in shell"
SHELL__NOTFOUND_STR: WORD "No such command."
SHELL__PROMPT_STR: WORD "$ "
; end globals
; begin func
SHELL_MAIN:
; begin prologue
  APUSH
; end prologue

  LD SHELL__START_STR_PTR
  FCALL ($PRINTLN_PTR)

  LD #30
  FCALL ($MALLOC_PTR)
  ST &input_ptr

  SHELL__LOOP:
    LD SHELL__PROMPT_STR_PTR
    FCALL ($PRINT_PTR)

    LD &input_ptr
    FCALL ($READ_LINE_PTR)

    LD &input_ptr
    CALL SHELL_SELECT_CMD

    ; begin if command not found
    BZC SHELL__LOOP_CONTINUE
      LD SHELL__NOTFOUND_STR_PTR
      FCALL ($PRINTLN_PTR) 
      JUMP SHELL__LOOP
    ; begin branch command found
    SHELL__LOOP_CONTINUE: 
      BMI SHELL__EXIT

      ST &ptr
      CALL (&ptr)
    ; end if

    JUMP SHELL__LOOP

SHELL__EXIT:
  LD &input_ptr
  FCALL ($MFREE_PTR)

; begin epilogue
  APOP
  RET
; end epilogue
; end func SHELL_MAIN



; func SHELL_SELECT_CMD: (cmd_str_ptr)
; 
; params: AC: cmd_str_ptr - command text pointer
; return: AC: cmd pointer to function to call or 0 if command not found
; stack:
; &: addr_ptr
; &: name_ptr
; &: cnt
; &: cmd_str_ptr
; &: cmd
; &: ret_addr
; begin func
SHELL_SELECT_CMD:
; begin prologue
  APUSH
; end prologue

  ST &cmd_str_ptr
  
  LD $_SHELL__CMD_LIST_CNT
  ST &cnt

  LD $_SHELL__CMD_ADDR_LIST_PTR
  ST &addr_ptr
  LD $_SHELL__CMD_NAME_PTR_LIST_PTR
  ST &name_ptr

  SSC__LOOP:
    LD (&name_ptr)+
    PUSH 
    LD &cmd_str_ptr+1
    FCALL ($STRCMP_PTR)
    SWAP
    POP

    BZS SSC__FOUND
    LD (&addr_ptr)+

    LOOP &cnt
    JUMP SSC__LOOP

  CLA
  JUMP SSC__END

  SSC__FOUND:
    LD (&addr_ptr)

  SSC__END:    
    ; ST &cmd no need now

; begin epilogue
  APOP
  RET
; end epilogue
; end func SHELL_SELECT_CMD



; func SHELL_CMD_HELP: () prints shell help
; stack:
; &: ptr
; &: iter
; begin globals
SCH__TEXT: WORD "Interactive shell.\r\nCommands are case-sensitive, non-letter symbols are prohibited.\r\nList: "
; end globals
; begin func
SHELL_CMD_HELP:
  APUSH

  LD SCH__TEXT_PTR
  FCALL ($PRINT_PTR)

  LD $_SHELL__CMD_LIST_CNT
  ST &iter

  LD $_SHELL__CMD_NAME_PTR_LIST_PTR
  ST &ptr

  SCH__LOOP:
    LD (&ptr)+
    FCALL ($PRINT_PTR)
    
    LD #0x20
    FCALL ($PRINT_CHAR_PTR)

    LOOP &iter
    JUMP SCH__LOOP

  FCALL ($PRINT_ENDL_PTR)

  APOP
  RET
; end func SHELL_CMD_HELP
